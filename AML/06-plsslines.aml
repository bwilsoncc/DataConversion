/************************************************
/* Creates coverages of corresponding feature classes plss lines
/* Args: None
/* 
/* Input:
/*   tmptaxmap3  lines are copied from here
/*   tmptaxbound    used for mapnumber
/* 
/* Output:
/*   tmpplssl lines
&WATCH 06-plsslines.wat

&do name &list tmpplssl, tmpline
  &if [exists %name% -cover] &then kill %name% all
&end

&type -- 1. Select just plss lines  from taxmap  ************************

/*  2  plss section line
/*  7  control reference line
/* 47  dlc line

reselect tmptaxmap2 tmpline line 
res taxmaptype = 7 OR taxmaptype = 2 or taxmaptype = 47 
[unquote ' '] 
n
n

&type -- 2. Intersect with tmptaxbound to get map number  ************************

intersect tmpline tmptaxbound tmpplssl line 
kill tmpline all

&type -- 3. add needed items ************************ 

additem TMPPLSSL.aat TMPPLSSL.aat linetype 3 3 i 
additem TMPPLSSL.aat TMPPLSSL.aat source 20 20 c 
additem TMPPLSSL.aat TMPPLSSL.aat sourcetype 20 20 c 
additem TMPPLSSL.aat TMPPLSSL.aat DIRECTION 12 12 C 

&type -- 4. assign fields as needed ************************

&data arc info 
arc
/********************************** Assign values to all 
sel TMPPLSSL.AAT 
MOVE 'OTHER' TO SOURCETYPE
MOVE 'OLDTAXMAPS' TO SOURCE 
CALC RELIACODE = 0
MOVE ANGLE TO DIRECTION
/******************************* SET LINE TYPES 
ASEL
RES TAXMAPTYPE = 7                                     /* gov lots
CALC LINETYPE = 46 
ASEL
RES TAXMAPTYPE = 2                                      /* PLS - section
CALC LINETYPE = 2
ASEL
RES TAXMAPTYPE = 47                                     /* DLC
CALC LINETYPE = 47
Q STOP
&END 

&type -- 5. DROP ITEMS NOT USED ***************************

DROPITEM TMPPLSSL.AAT TMPPLSSL.AAT               
TMPLINE#
TMPLINE-ID
ANGLE
TAXMAPSYM
TAXMAPTYPE
GEOTRANSNO
AREA
PERIMETER
ORMAPNUM
[UNQUOTE ' '] 
Y
Y

&type -- That's all --
&RETURN 
