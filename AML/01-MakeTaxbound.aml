/*
/* Split out of MakeMapIndex so that I could run some python on Taxbound.
/*
/* Args:  source      workspace containing read-only coverages
/*
/* Input:
/*   taxmap      coverage from the %source%
/*
/* Output:
/*   tmptaxmap1  copy of the source taxmap coverage, which we create here
/*   taxbound    map and detail map polygons copied from taxmap
/*
/********************************************************************************
&args source
/* typically run as:  &r 01-maketaxbound.aml %PROJECTHOME%/Source/t4-6

&WATCH 01-MakeTaxbound.wat
DISPLAY 9999 1

/***************************************************************************
/* 1. delete coverages that will be created

&do name &list tmptaxmap1, taxbound
  &if [exists %name% -cover] &then kill %name% all
&end

copy %source%\taxmap tmptaxmap1

/* I take it we don't want whatever these things are...??

&s X = [delete tmptaxmap1.acode -info]
&s x = [delete tmptaxmap1.tatatigds -info]

/* 1.5 - add tmptaxmap annotation attributes

addtext tmptaxmap1 igds 
additem tmptaxmap1.tatigds tmptaxmap1.tatigds maplevel 3 3 i 
additem tmptaxmap1.tatigds tmptaxmap1.tatigds mapsize 8 8 n 2 
additem tmptaxmap1.tatigds tmptaxmap1.tatigds mapscale 5 5 i
additem tmptaxmap1.tatigds tmptaxmap1.tatigds mapnumber 20 20 c 

/* Set coordinate precision

PRECISION DOUBLE DOUBLE 

/* 2. Copy map (51) and detail map (52) polygons to taxbound coverage

arcedit 
ec tmptaxmap1

ef arc
sel taxmaptype = 51 or taxmaptype = 52
put taxbound

ef label
sel taxmaptype = 51 or taxmaptype = 52
put taxbound

/* save  /* --- should not need to modify tmptaxmap1
q 

clean taxbound # 10 .1

/* 3. Add fields to POINT attributes (.PAT)

/* I added PAGENAME so that I can use it later in DDP queries
/* to restrict what annotation shows up on a map. (Specifically for SEEMAP)
/* It will be populated from the python that calls this AML.

additem TAXBOUND.PAT TAXBOUND.PAT  MAPSCALE 5 5 I 
additem TAXBOUND.PAT TAXBOUND.PAT  MAPNUMBER 20 20 C 
additem TAXBOUND.PAT TAXBOUND.PAT  PAGENUMBER 3 3 I
additem TAXBOUND.PAT TAXBOUND.PAT  PageName 24 24 C
additem TAXBOUND.PAT TAXBOUND.PAT  ORMAPNUM 24 24 C 
additem TAXBOUND.PAT TAXBOUND.PAT  MAPSUFTYPE 1 1 C 
additem TAXBOUND.PAT TAXBOUND.PAT  MAPSUFNUM 4 4 I 

additem TAXBOUND.PAT TAXBOUND.PAT  RELIACODE 2 2 I 
additem TAXBOUND.PAT TAXBOUND.PAT  AUTODATE 8 10 D  
additem TAXBOUND.PAT TAXBOUND.PAT  AUTOMETHOD 3 3 C  
additem TAXBOUND.PAT TAXBOUND.PAT  AUTOWHO 30 30 C 
additem TAXBOUND.PAT TAXBOUND.PAT  CITYNAME 50 50 C 

&DATA ARC INFO
ARC
SEL TAXBOUND.PAT 
RED
45,MQ,1,1,C 
46,MQQ,1,1,C 

Q STOP
&END 

/* 4. calculate map number information
/* Used to be called "3. CALCULATE SCALE FOR TAXDETAIL"
/* which seems more accurate to me.
/* MAPSUFTYPE -- in Dean's version '---' which makes no sense to me

&DATA ARC INFO
ARC
SEL TAXBOUND.PAT 
MOVE '0' TO MAPSUFTYPE
MOVE '000' TO MAPSUFNUM 
CALC PAGENUMBER = 1

CALC AUTODATE = AUTO-DATE
MOVE AUTO-METHOD TO AUTOMETHOD 
CALC RELIACODE = 0 
MOVE AUTO-WHO TO AUTOWHO 

MOVE MAPNUM TO MAPNUMBER
CONCACT ORMAPNUM FROM '040',TOWN,'.00','N',RANGE,'.00','W',SECTION,QTR,'--0000'

/* Set the scale based on section, qsection, qqsection
CALC MAPSCALE = 2000
RES SECTION > 0 
CALC MAPSCALE = 400
RES QTR NC '00' 
CALC MAPSCALE = 200
RES QTR NC '0'
CALC MAPSCALE = 100


Q STOP
&END 

/* +--------- Brian pasted this section in from ConvertAMLS
/* |
/* 4. CALCULATE DETAIL AND ORMAPNUM FOR DETAILS 
/* NOTE.... THIS IS STRANGE SECTION. CLATSOP DOES NOT HAVE DETAILS OR SUFFIX MAPS 
/* NUMBERED. SO I ADDED A SUFFIX NUMBER TO THE FILE AND NOW I HAVE TO GO THRU MAP 
/* NUMBER BY MAP NUMBER AND ASSIGN THEM A NUMBER. I DID THIS USING AN INFO PROGRMA
/*
/* In Clatsop,
/* Township DIR is ALWAYS N
/* Range DIR is ALWAYS W

&DATA ARC INFO 
ARC
SEL TAXBOUND.PAT 
ERASE CALCSUFNUM
Y
PROGRAM CALCSUFNUM
FORMAT $CHR10,10,10,C
FORMAT $NUM1,4,4,I
SEL TAXBOUND.PAT
RES DETAILSCALE > 0
CALC MAPSCALE = DETAILSCALE
MOVE 'D' TO MAPSUFTYPE 
SORT MAPNUM
CALC $NUM1 = 1
MOVE ' ' TO $CHR10
PROGRAM SECTION 2
CALC MAPSUFNUM = 1
IF MAPNUM = $CHR10
  CALC $NUM1 = $NUM1 + 1
  CALC MAPSUFNUM = $NUM1
  CALC PAGENUMBER = MAPSUFNUM
ENDIF
IF MAPNUM NE $CHR10
  CALC $NUM1 = 1
  CALC MAPSUFNUM = 1
  CALC PAGENUMBER = MAPSUFNUM 
ENDIF
MOVE MAPNUM TO $CHR10
PROGRAM SECTION 2N
ASEL
SORT TAXBOUND#

RUN CALCSUFNUM

ASEL
RES DETAILSCALE > 0
CONCACT ORMAPNUM FROM '040',TOWN,'.00','N',RANGE,'.00','W',SECTION,QTR,'--D',MAPSUFNUM

Q STOP
&END
/* |
/* +---------

/* 7. Change leading spaces (due for example to RANGE being 1 digit) to zeros

ARCEDIT 
EC TAXBOUND
EF LABEL 
SEL ALL
CHANGE ORMAPNUM C/ /0/G 
SAVE 
Q  

&RETURN
