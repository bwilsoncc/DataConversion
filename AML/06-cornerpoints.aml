/************************************************
/* Creates coverages of corresponding feature class-> corner points
/* 
/* Input:
/*   tmptaxbound
/*   tmptaxmap2
/*   DLCMASTER from AnnoGenCov
/*   xxxyyy.lut from K:   I have no idea what this is for, I asked Dean but I don't remember the answer :-)
/*
/* Output:
/*   tmpcorner       annotation and points
/*   xxxyyy.lut   gets copied into our workspace
/*
&WATCH 06-cornerpoints.wat

&if [exists tmppt -cover] &then kill tmppt all 
&if [exists tmpcorner -cover] &then kill tmpcorner all 
&s x = [delete DLCMASTER -INFO] 
&s x = [delete xxxyyy.lut -info] 

&if [exists control -cover] &then kill control all 
/* &S DLCMASTERPATH = P:\taxmaped\apps\ConvertTwnToGeo\annogencov 
&S DLCMASTERPATH = C:\GeoModel\Clatsop\AnnoGenCov

/* COPYINFO P:\taxmaped\apps\biged2\lookupPolk\INFO!ARC!XXXYYY.LUT
COPYINFO K:\taxmaped\apps\biged2\lookupclatsop\INFO!ARC!XXXYYY.LUT

/* COPYINFO %DLCMASTERPATH%\INFO!ARC!DLCMASTER DLCMASTER
/* &if ^ [exists %DLCMASTERPATH% -DIRECTORY] &then &do 
/*   &message &popup 
/*   &type 'ERROR - PATH TO DLCMASTER TABLE
/*   &message &on
/*   &return ; &return 
/* &end 

&if ^ [exists tmptaxbound -cover] &then &do 
  &message &popup 
  &type 'ERROR - MISSING TMPTAXBOUND'
  &message &on
  &return ; &return 
&end 

&type -- 1. Select just ref lines  from taxmap  ************************
/*20-HWYSTATION
/*45-SECTION
/*44-SUBSECTION
/*47-DLC
/*48-MEANDERCO

reselect tmptaxmap2 tmppt POINT 
res taxmaptype = 20 OR taxmaptype = 44 or taxmaptype = 45 OR TAXMAPTYPE = 47 ~
OR TAXMAPTYPE = 48 
[unquote ' '] 
n
n

&type -- 2. Intersect with tmptaxbound to get map number  ************************
intersect tmppt tmptaxbound tmpcorner  point
kill tmppt all

&type -- 3. Add needed items ************************ 

additem tmpcorner.Pat tmpcorner.Pat CornerSymbol 3 3 i 
additem tmpcorner.Pat tmpcorner.Pat CornerDesc 64 64 c 
additem tmpcorner.Pat tmpcorner.Pat ControlType 3 3 i 
additem tmpcorner.Pat tmpcorner.Pat CommonName 32 32 c 
additem tmpcorner.Pat tmpcorner.Pat GCDB 10 10 c 
additem tmpcorner.Pat tmpcorner.Pat Easting 8 18 F 5 
additem tmpcorner.Pat tmpcorner.Pat Northing 8 18 F 5 
additem tmpcorner.Pat tmpcorner.Pat Latitude 8 18 F 5 
additem tmpcorner.Pat tmpcorner.Pat Longitude 8 18 F 5 

additem tmpcorner.Pat tmpcorner.Pat source 20 20 c 
additem tmpcorner.Pat tmpcorner.Pat sourcetype 20 20 c 

&type -- 4. Assign fields as needed ************************

&data arc info 
arc
/********************************** Assign values to all 
sel TMPCORNER.PAT
MOVE 'OTHER' TO SOURCETYPE
MOVE 'OLDTAXMAPS' TO SOURCE 
CALC RELIACODE = 0
MOVE POINTNAME TO COMMONNAME
/******************************* SET POINT TYPES 
ASEL
RES TAXMAPTYPE = 20                                     /* Station-corner
CALC CORNERSYMBOL = 1 
CALC CONTROLTYPE = 24
CONCAT COMMONNAME FROM 'STN-',POINTNAME
ASEL
RES TAXMAPTYPE = 45                                      /* Section
CALC CORNERSYMBOL = 0 
CALC CONTROLTYPE = 22
CONCAT CORNERDESC FROM 'SCTN-',POINTNAME
ASEL
RES TAXMAPTYPE = 44                                     /* subsection
CALC CORNERSYMBOL = 0 
CALC CONTROLTYPE = 21
CONCAT CORNERDESC FROM 'SUBSCTN-',POINTNAME
ASEL
RES TAXMAPTYPE = 47                                     /* DLC
CALC CORNERSYMBOL = 5 
CALC CONTROLTYPE = 7
CONCAT CORNERDESC FROM 'DLC-',POINTNAME
ASEL
RES TAXMAPTYPE = 48                                     /* MEANDER
CALC CORNERSYMBOL = 7 
CALC CONTROLTYPE = 17
CONCAT CORNERDESC FROM 'MNDR-',POINTNAME'
Q STOP
&END 

&DATA ARC INFO
ARC
SEL TMPCORNER.PAT
RES POINTNAME CN '0' AND POINTNAME NC 'd' AND POINTNAME NC 'c'and POINTNAME NC 'D' and POINTNAME NC 'S' 
MOVE POINTNAME TO GCDB 
Q STOP
&END 


&type -- 5. DROP ITEMS NOT USED ***************************

DROPITEM TMPCORNER.PAT TMPCORNER.PAT 
TMPPT#
TMPPT-ID
IGDS-GGNO
MAPNUM
TOWN
RANGE
SECTION
QTR
TAXLOT
REFOREST
TAXCODE
POINTNAME
CLOSEMETHOD
CLOSERROR
LEGACRES1
LEGACRES2
MAPACRES
GEOTRANSNO
TAXMAPTYPE
TAXMAPSYM
TMPTAXBOUND#
TMPTAXBOUND-ID
ORMAPNUM
MAPSUFTYPE
MAPSUFNUM
TAXLOTC
MQ
MQQ
[UNQUOTE ' '] 
Y
Y

&type -- That's all! --
&RETURN 
/********************************************************************
