/************************************************
/* Creates coverages of corresponding feature classes RefLines lines
/*
/* Args: NONE
/*
/* Input:
/*   tmptaxmap2
/*   taxbound
/* 
/* Output:
/*   refline

&WATCH 06-reflines.wat

&if [exists tmpline -cover] &then kill tmpline all 
&if [exists refline -cover] &then kill refline all 

&if ^ [exists taxbound -cover] &then &do 
  &message &popup 
  &type 'ERROR - MISSING TAXBOUND'
  &message &on
  &return ; &return 
&end 

&type -- 1. Select just ref lines  from tmptaxmap3  ************************

reselect tmptaxmap2 tmpline line 
res taxmaptype = 33 OR taxmaptype = 9 or taxmaptype = 40 OR TAXMAPTYPE = 15 OR TAXMAPTYPE = 17 or taxmaptype = 18 
[unquote ' '] 
n
n

&type -- 2. Intersect with taxbound to get map number  ************************

/* This throws a bunch of "item duplicated" warning messages that seem harmless.
intersect tmpline taxbound refline line
kill tmpline all

&type -- 3. add needed items ************************ 

additem REFLINE.aat REFLINE.aat linetype 3 3 i 
additem REFLINE.aat REFLINE.aat source 20 20 c 
additem REFLINE.aat REFLINE.aat sourcetype 20 20 c 
additem REFLINE.aat REFLINE.aat LoStation 20 20 c 
additem REFLINE.aat REFLINE.aat HiStation 20 20 c 
additem REFLINE.aat REFLINE.aat DIRECTION 12 12 c 

&type -- 4. assign fields as needed ************************

/********************************** Assign values to all 
&data arc info 
arc
sel REFLINE.AAT 
MOVE 'OTHER' TO SOURCETYPE
MOVE 'OLDTAXMAPS' TO SOURCE 
CALC RELIACODE = 0
move ANGLE TO DIRECTION 
/******************************* SET LINE TYPES 
ASEL
RES TAXMAPTYPE = 33                                     /* historical
CALC LINETYPE = 33 
ASEL
RES TAXMAPTYPE = 9                                      /* Road Centerline
CALC LINETYPE = 9
ASEL
RES TAXMAPTYPE = 40                                     /* Subdivison
CALC LINETYPE = 40
ASEL
RES TAXMAPTYPE = 15                                     /* Railroad Centerline
CALC LINETYPE = 15
ASEL
RES TAXMAPTYPE = 17                                     /* Easement ROW
CALC LINETYPE = 17
ASEL
RES TAXMAPTYPE = 18                                     /* Easement CL
CALC LINETYPE = 18
Q STOP
&END 

&type -- 5. DROP ITEMS NOT USED ***************************

DROPITEM REFLINE.AAT REFLINE.AAT               
TMPLINE#
TMPLINE-ID
ANGLE
TAXMAPSYM
TAXMAPTYPE
TAXBOUND-ID
TAXBOUND#
AREA
PERIMETER
IGDS-GGNO
MAPNUM
TOWN
RANGE
SECTION
QTR
TAXLOT
REFOREST
TAXCODE
CLOSEMETHOD
CLOSERROR
LEGACRES1
LEGACRES2
MAPACRES
POINTNAME
GEOTRANSNO
PAGENUMBER
ORMAPNUM
MAPSUFTYPE
MAPSUFNUM
TAXLOTC
MQ
MQQ
[UNQUOTE ' '] 
Y
Y

&type -- That's all --
&RETURN 
/********************************************************************
LINEFEATURE
33-HISTORY
9-ROAD-CL
40-SUBDIV
15-RR-CL
17-ESMT-ROW
