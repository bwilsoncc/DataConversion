/************************************************
/* Creates coverages of corresponding feature classes water lines
/*
/* steps 08, 09, 10, 11, 12 could probably all be refactored into one step
/* 
/* Input:
/*   tmptaxmap2  lines are copied from here
/*   tmptaxbound    lines are copied from here
/* 
/* Output:
/*   tmpwaterl    will be deleted and recreated
/*   The coverage will have annotation.igds but we don't use it so don't worry about the extra anno in there.

&WATCH 06-waterlines.wat

&if ^ [exists tmptaxbound -cover] &then &do 
  &message &popup 
  &type 'ERROR - MISSING TMPTAXBOUND'
  &message &on
  &return ; &return 
&end 

&do name &list tmpline, tmpwaterl
  &if [exists %name% -cover] &then kill %name% all
&end

&type -- 1. Select just waterlines from taxmap  ************************

reselect tmptaxmap2 tmpline line 
res taxmaptype = 22 OR taxmaptype = 24 or taxmaptype = 26 or taxmaptype = 28 
[unquote ' '] 
n
n

&type -- 2. Intersect with tmptaxbound to get map number  ************************

intersect tmpline tmptaxbound tmpwaterl line 
kill tmpline all

&type -- 3. add needed items ************************ 

additem tmpwaterl.aat tmpwaterl.aat watername 64 64 c 
additem tmpwaterl.aat tmpwaterl.aat linetype 3 3 i 
additem tmpwaterl.aat tmpwaterl.aat source 20 20 c 
additem tmpwaterl.aat tmpwaterl.aat sourcetype 20 20 c 

&type -- 4. assign fields as needed ************************

&data arc info 
arc
/********************************** Assign values to all 
sel TMPWATERL.AAT 
MOVE 'OTHER' TO SOURCETYPE 
MOVE 'OLD TAXMAPS' TO SOURCE 
/******************************* SET LINE TYPES 
ASEL
CALC LINETYPE = TAXMAPTYPE              /* water LINE VALUES/types match !!!! 
Q STOP
&END 

&type -- 5. DROP ITEMS NOT USED ***************************

DROPITEM TMPWATERL.AAT TMPWATERL.AAT               
TMPLINE#
TMPLINE-ID
ANGLE
DISTANCE
RADIUS
DELTA
TANGENT
ARCLENGTH
SIDE
RADIUS2
TANGENT2
TAXMAPSYM
TAXMAPTYPE
GEOTRANSNO
TMPTAXBOUND#
AREA
PERIMETER
TMPTAXBOUND-ID
IGDS-GGNO
MAPNUM
TOWN
RANGE
SECTION
QTR
TAXLOT
REFOREST
TAXCODE
DETAILSCALE
CLOSEMETHOD
CLOSERROR
LEGACRES1
LEGACRES2
MAPACRES
POINTNAME
ORMAPNUM
MAPSUFTYPE
MAPSUFNUM
TAXLOTC
MQ
MQQ
[UNQUOTE ' '] 
Y
Y

&type -- That's all! --
&RETURN 
/********************************************************************

>100 - ARROWS 
100-ARROWS
032-TAXLOT
033-HISTORY
008-ROAD-ROW
009-ROAD-CL
040-SUBDIV
014-RR-ROW
015-RR-CL
022-WATER-BDY
024-RIVER
026-CREEK
008-GOV-LOTS
002-PLS
047-DLC
053-TAXCODE
062-MAPBOUND
034-BRG/DIST
017-ESMT-ROW
034-HOOK
034-LAND-HOOK
034-DIM-TIC

