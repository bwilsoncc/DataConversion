/* Generate a coverage containing an annotation feature class
/*
/* Args:
/*   COVERAGE   Name of coverage (a string)
/*   SELECTION  A selection expression (a string) for example: $SYMBOL = 62
/*   (this expression can be complex; eg: $SYMBOL <> 37 AND $SYMBOL <> 54 AND $SYMBOL <> 62)
/* 
/* Input:
/*   tmptaxmap2 anno.igds
/*
/* Output:
/*   the named coverage
/* 
&args COVERAGE QUOTEDSELECTION:REST
&s SELECTION = [unquote %QUOTEDSELECTION%]
&s TEMPLATE = C:/GeoModel/Clatsop/AnnoTemplateCoverages/taxmapanno

&echo &on

&type COVERAGE  = %COVERAGE%
&type SELECTION = %SELECTION%

&do name &list tmptaxmap2 %TEMPLATE%
  &if ^ [exists %name% -cover] &then &do 
    &type ERROR -- Missing %name%
    &return ; &return
  &end
&end

/* &s TMPCOVERAGE = tmp%COVERAGE%
/* &if [exists %TMPCOVERAGE% -cover] &then kill %TMPCOVERAGE% all
&if [exists %COVERAGE% -cover] &then kill %COVERAGE% all

arcedit
ec tmptaxmap2
ef anno.igds

/* Some of our data has the symbol set wrong (38?) but the text says SEE MAP so I try to catch that.
SEL %SELECTION%
&if [show number select] > 0 &then &do
  put %COVERAGE%
&end
q

&if [exists %COVERAGE% -cover] = .false. &then &do
  &type No symbol matches for %COVERAGE% %SELECTION%
  &return ; &return
&end

/* Remember things that look fragmented might be -- two features like "SEE MAP" and "6 10 29DB" will draw together

/* ========================================================================
&type -- Assign new levels based on scale.
/* I don't think I even need to do this, since I just use
/* mapscale directly to determine if anno shows or not at a given scale.

&type -- Assign new LEVEL based on scale, but DON'T change the symbol.

/* Not even sure I need to do this, since I just use mapscale
/* directly in ArcMap to determine if anno shows or not at a given scale.

arcedit 
ec %COVERAGE%
ef anno.igds

/* All the large scales get tossed into one level, 0.
sel mapscale < 100
&if [show number select] > 0 &then &do 
  calc $LEVEL = 0
  calc $SYMBOL = 0
&end

nsel
&if [show number select] > 0 &then &do
  /* This catches the leftover cases where scale is not a known value, "this should never happen!"
  /* All scales >= 100 get set to 5 here, then known scales are set to correct values below.
  calc $LEVEL = 5
  calc $SYMBOL = 5
&end

sel mapscale = 100
&if [show number select] > 0 &then &do
  &type SCALE 100
  calc $LEVEL = 1
  calc $SYMBOL = 1
&end

sel mapscale = 200 
&if [show number select] > 0 &then &do 
  &type SCALE 200
  calc $LEVEL = 2
  calc $SYMBOL = 2
&end

sel mapscale = 400 
&if [show number select] > 0 &then &do 
  &type SCALE 400
  calc $LEVEL = 3
  calc $SYMBOL = 3
&end

sel mapscale = 2000 
&if [show number select] > 0 &then &do
  &type SCALE 2000
  calc $LEVEL = 4
  calc $SYMBOL = 4
&end

save
q

/* ========================================================================

&type Drop unwanted fields
dropitem %COVERAGE%.tatigds %COVERAGE%.tatigds 
X
Y
OFFSETX
OFFSETY
HEIGHT
TOWN
RANGE
SECTION
QTR
GEOTRANSNO
[UNQUOTE ' '] 
Y
Y

/* ========================================================================

/* Now to get the proper annotation classes,
/* create the final anno coverage by appending the template and our data

/* arcedit
/* ec %TEMPLATE%
/* ef anno.igds
/* get %TMPCOVERAGE% 
/* put %COVERAGE%
/* Q
/* &if [exists %TMPCOVERAGE% -cover] &then kill %TMPCOVERAGE% all

/* We are done with this stupid hack
dropitem %COVERAGE%.tatigds %COVERAGE%.tatigds SYMBOL

&type Built %COVERAGE% annotation!
&return
