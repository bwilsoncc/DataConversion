/* Generate a coverage containing an annotation feature class
/*
/* Args:
/*   TMPL8      Name of a template file
/*   COVERAGE   Name of coverage (a string)
/*   SELECTION  A selection expression (a string) for example: $SYMBOL = 62
/*   (this expression can be complex; eg: $SYMBOL <> 37 AND $SYMBOL <> 54 AND $SYMBOL <> 62)
/* 
/* Input:
/*   tmptaxmap2 anno.igds
/*
/* Output:
/*   the named coverage
/* 
&args TMPL8 COVERAGE QUOTEDSELECTION:REST

&s SELECTION = [unquote %QUOTEDSELECTION%]
&s TEMPLATE = C:/GeoModel/Clatsop/AnnoTemplateCoverages/%TMPL8%

&type COVERAGE  = %COVERAGE%
&type SELECTION = %SELECTION%

&do name &list tmptaxmap2 %TEMPLATE%
  &if ^ [exists %name% -cover] &then &do 
    &type ERROR -- Missing %name%
    &return ; &return
  &end
&end

&if [exists tmpanno -cover] &then kill tmpanno all
&if [exists %COVERAGE% -cover] &then kill %COVERAGE% all

arcedit
ec tmptaxmap2
ef anno.igds
SEL %SELECTION%
&if [show number select] > 0 &then &do
put tmpanno
&end
q

&if [exists tmpanno -cover] = .false. &then &do
  &type ================= WARNING! NOT creating %COVERAGE% ===================
  &return
&end

/* Remember things that look fragmented might be -- two features like "SEE MAP" and "6 10 29DB" will draw together

/* ========================================================================
&type -- Assign new levels based on scale.
/* $LEVEL is imported into geodatabase as annotation classes

arcedit 
ec tmpanno
ef anno.igds

/* All the large scales get tossed into one level, 0.
sel mapscale < 100
&if [show number select] > 0 &then &do 
  calc $LEVEL = 0
&end

nsel
&if [show number select] > 0 &then &do
  /* This catches the leftover cases where scale is not a known value, "this should never happen!"
  /* All scales >= 100 get set to 5 here, then known scales are set to correct values below.
  calc $LEVEL = 5
&end

sel mapscale = 100
&if [show number select] > 0 &then &do
  &type SCALE 100
  calc $LEVEL = 1
&end

sel mapscale = 200 
&if [show number select] > 0 &then &do 
  &type SCALE 200
  calc $LEVEL = 2
&end

sel mapscale = 400 
&if [show number select] > 0 &then &do 
  &type SCALE 400
  calc $LEVEL = 3
&end

sel mapscale = 2000 
&if [show number select] > 0 &then &do
  &type SCALE 2000
  calc $LEVEL = 4
&end

save
q

/* ========================================================================

&type Drop unwanted fields
dropitem tmpanno.tatigds tmpanno.tatigds 
X
Y
OFFSETX
OFFSETY
HEIGHT
TOWN
RANGE
SECTION
QTR
GEOTRANSNO
MAPSIZE
[UNQUOTE ' '] 
Y
Y

/* ========================================================================

/* Now to get the proper annotation classes,
/* create the final anno coverage by appending the template and our data

copy %TEMPLATE% %COVERAGE%
arcedit
ec %COVERAGE%
ef anno.igds
get tmpanno 
save
quit yes

&if [exists tmpanno -cover] &then kill tmpanno all

/* We are done with this stupid hack so get rid of the duplicated field
dropitem %COVERAGE%.tatigds %COVERAGE%.tatigds SYMBOL

&type Built %COVERAGE% annotation!
&return
