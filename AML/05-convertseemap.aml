/* Format attributes so that they can be seen in ArcGIS
/* including $size and some kind of maplevel thing
/*
/* Args: NONE
/*
/* Input:
/*   tmpseemap   annotation.igds
/*   tmptaxmap2  annotation.igds
/*
/* Output:
/*   taxseemap   annotation.igds      (final)
/*   tmptaxmap3  annotation.igds      taxseemap entries get added
/* 
&WATCH 05-convertseemap.wat

&do name &list tmptaxseemap, tmptaxmap2
  &if ^ [exists %name% -cover] &then &do 
    &type Missing %name%
    &return ; &return
  &end
&end

&do name &list tmptaxmap3, taxseemap
  &if [exists %name% -cover] &then kill %name% all
&end

&type -- Copy the coverages we will modify

copy tmptaxmap2 tmptaxmap3
copy tmptaxseemap taxseemap

&type -- 1. Save annotation information in fields
/* I am pretty sure this entire step is redundant now.

arcedit
display 9999 1
/* At this point in Polk county version, annotation is in taxmap/annotation.seemap.
/* Clatsop is still using a separate coverage.
ec taxseemap
ef anno.igds
sel all
calc mapsize = $size
save
q 

&type -- 2. Create taxmap number xref 

/* 2/13/18
/* "maplevel" field has been superceded, it's just left over from earlier versions
/* Doesnt hurt anything but you probably could remove it if you are feeling daring

&DATA ARC INFO
ARC
SEL MAPLEVEL
ERASE MAPLEVEL 
Y
DEFINE MAPLEVEL
MAPNUM,10,10,C
TOWN,2,2,I
RANGE,2,2,I
SECTION,2,2,I
QTR,2,2,C
LEVEL,8,8,I

SEL TAXBOUND.PAT
SORT MAPNUM
RELATE MAPLEVEL BY MAPNUM WITH INIT
CALC $1TOWN = TOWN 
CALC $1RANGE = RANGE 
CALC $1SECTION = SECTION 
MOVE QTR TO $1QTR
ASEL
SORT TAXBOUND# 
SEL MAPLEVEL 
RED 
16,S,1,1,I 

ASEL
RES SECTION < 10
CONCAT LEVEL FROM TOWN,RANGE,S,QTR 
NSEL 
CONCAT LEVEL FROM TOWN,RANGE,SECTION,QTR 
ALTER LEVEL 
,,C,,,,,,,,,,

Q STOP
&END 

&type -- Change a reference to A|B|C|D to 1|2|3|4 in the LEVEL field.
arcedit
ec maplevel info
sel all
/* STRING manipulation, C/OLDPATTERN/NEWPATTERN/GLOBAL
change LEVEL C/A/1/G 
change LEVEL C/B/2/G 
change LEVEL C/C/3/G
change LEVEL C/D/4/G
save
q

&DATA ARC INFO
ARC
SEL MAPLEVEL
ALTER LEVEL
,,I,,,,,,,,,,,,,,
CALC LEVEL = LEVEL 
RES SECTION < 10 
CALC LEVEL = LEVEL / 10


SEL TAXSEEMAP.TATIGDS
ALTER LEVEL
,10,,,,,,,,,,,,,,
RELATE MAPLEVEL BY LEVEL WITH NUMERIC
CALC $NM = 1
RES LEVEL = $1LEVEL
MOVE $1MAPNUM TO MAPNUMBER                     
                                            /* MAKE MAPSCALE LARGE IF NO MATCH 
CALC MAPSCALE = 2000
RES $1SECTION > 0 
CALC MAPSCALE = 400
RES $1QTR NC '00' 
CALC MAPSCALE = 200
RES $1QTR NC '0'
CALC MAPSCALE = 100

Q STOP
&END 

&type -- 6. Calc feature types to standard and save in tmptaxmap3 anno

arcedit

ec taxseemap 
ef anno.igds
sel all

/* 62 is "See maps" 
calc $LEVEL = 62 
calc $SIZE = 0 
calc $SYMBOL = 62 
calc LEVEL = 62
save

/* Now put it back in taxmap anno for subsequent stages
/* Pretty sure this is totally pointless
/* I think it would need to be done before the anno conversion (step 04) to have any effect

ec tmptaxmap3
ef anno.igds
get taxseemap
save 
q

&type -- That's all! --
&return
