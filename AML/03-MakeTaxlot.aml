/* See FIXME, we're putting 0 in acres due to an ARC overflow error
/* I think I can either ignore that or fix it in python later.

/* This script is IDEMPOTENT,
/* it does not change inputs,
/* it creates taxlot and taxlotpt

/*  Dean 10/24/05
/********************************************************************************
/* Args:
/*   source     workspace containing read-only source data
/* 
/* Input:
/*   %source%/taxlot
/*   tmptaxmap1
/* 
/* Output:
/*   taxlot   -- arc, point, label pt feature classes
/*   taxlotpt -- it's the points from the "taxlot" label fc

&args source
/* For example, &r 03-maketaxlot.aml %PROJECTHOME%/Source/t4-6

&WATCH 03-MakeTaxlot.wat
&ECHO &ON 
&term 9999 

/***************************************************************************
/* 1. delete coverages you will create and check if inputs are present 

&if ^ [exists tmptaxmap1 -cover] &then &do 
 &type No tmptaxmap1, did you run 01-MakeMapIndex.aml??
  &return ; &return 
&end 

&do name &list taxlot, taxlotpt, taxlotbk
  &if [exists %name% -cover] &then kill %name% all
&end

/* This gets deleted when we finish
copy %source%/taxlot taxlotbk 

/* 2. Create taxlot coverage 

PRECISION DOUBLE DOUBLE 

create taxlot 
build taxlot line
build taxlot point 
tolerance taxlot FUZZY .01 

/* 3. Add point attribute fields

additem taxlot.pat taxlot.pat COUNTY,2,2,I 
additem taxlot.pat taxlot.pat TOWN,2,2,I 
additem taxlot.pat taxlot.pat TOWNPART, 3,3,N,2
additem taxlot.pat taxlot.pat TOWNDIR,1,1,c 
additem taxlot.pat taxlot.pat RANGE,2,2,I                          
additem taxlot.pat taxlot.pat RANGEPART,3,3,n,2              
additem taxlot.pat taxlot.pat RANGEDIR ,1,1,c  
additem taxlot.pat taxlot.pat SECTION,2,2,I             
additem taxlot.pat taxlot.pat SECNUMBER,2,2,I  
additem taxlot.pat taxlot.pat QTR, 2,2 ,C             
additem taxlot.pat taxlot.pat STQTR,1,1,c                            
additem taxlot.pat taxlot.pat QTRQTR,1,1,c                    
additem taxlot.pat taxlot.pat ANOMALY,2,2,c                
additem taxlot.pat taxlot.pat MAPSUFTYPE ,1,1,c         
additem taxlot.pat taxlot.pat MAPSUFNUM,4,4,I            
additem taxlot.pat taxlot.pat MAPNUMBER,20,20,c
additem taxlot.pat taxlot.pat MAPNUM,10,10,C 
additem taxlot.pat taxlot.pat ORMAPNUM,24,24,c        
additem taxlot.pat taxlot.pat TAXLOT,5,5,c                       
additem taxlot.pat taxlot.pat SPECIALINT,1,1,c               
additem taxlot.pat taxlot.pat MAPTAXLOT ,25,25,c          
additem taxlot.pat taxlot.pat ORTAXLOT,29,29,c  
additem taxlot.pat taxlot.pat TAXLOTFEET,8,8,I 
additem taxlot.pat taxlot.pat TAXLOTACRE,8,8,N,2 
additem taxlot.pat taxlot.pat MAPACRES,8,8,N,2 
additem taxlot.pat taxlot.pat RELIACODE,2,2,I 
additem taxlot.pat taxlot.pat AUTODATE,8,10,D
additem taxlot.pat taxlot.pat AUTOMETHOD,3,3,C 
additem taxlot.pat taxlot.pat AUTOWHO,30,30,C 
additem taxlot.pat taxlot.pat TAXMAPKEY,12,12,C 
additem taxlot.pat taxlot.pat LEGACRES1 9 9 N 2 

additem taxlot.pat taxlot.pat TAXMAPTYPE,3,3,I 
additem taxlot.pat taxlot.pat AUTO-WHO,3,3,C 
additem taxlot.pat taxlot.pat AUTO-METHOD,3,3,C 
additem taxlot.pat taxlot.pat AUTO-DATE,8,10,D

/* Add taxlot line attributes

additem taxlot.aat taxlot.aat TAXMAPTYPE,3,3,I 
additem taxlot.aat taxlot.aat AUTO-WHO,3,3,C 
additem taxlot.aat taxlot.aat AUTO-METHOD,3,3,C 
additem taxlot.aat taxlot.aat AUTO-DATE,8,10,D 
additem taxlot.aat taxlot.aat AUTO-REL,1,1,I 
 
additem taxlot.aat taxlot.aat LINETYPE,3,3,I
additem taxlot.aat taxlot.aat DIRECTION,12,12,C 
additem taxlot.aat taxlot.aat DISTANCE,10,10,C
additem taxlot.aat taxlot.aat ANGLE,10,10,C
additem taxlot.aat taxlot.aat DELTA,10,10,C
additem taxlot.aat taxlot.aat RADIUS,10,10,C 
additem taxlot.aat taxlot.aat RADIUS2,10,10,C
additem taxlot.aat taxlot.aat TANGENT,10,10,C 
additem taxlot.aat taxlot.aat TANGENT2,10,10,C
additem taxlot.aat taxlot.aat ARCLENGTH,10,10,C 
additem taxlot.aat taxlot.aat SIDE,1,1,C
additem taxlot.aat taxlot.aat SOURCE,20,20,C
additem taxlot.aat taxlot.aat SOURCETYPE,20,20,C
additem taxlot.aat taxlot.aat RELIACODE,2,2,I 
additem taxlot.aat taxlot.aat AUTODATE,8,10,D
additem taxlot.aat taxlot.aat AUTOMETHOD,3,3,C 
additem taxlot.aat taxlot.aat AUTOWHO,30,30,C 


/* 4. Calc values 

arcedit

/* Select the lines from tmptaxmap1 that will be copied into taxlot lines
ec tmptaxmap1
ef arc

/* 8  road
/* 14 rxr
/* 31 taxlot labels (apparently)
/* 32 parcel
/* 51 map boundary

/* I don't know why taxmaptype 51 is in here, it seems wrong to me but I am going with it.
sel taxmaptype = 8 or taxmaptype = 14 or taxmaptype = 32 or taxmaptype = 51

put taxlot
/* Coverage already exists. Do you want to append?  (we just created it up above so this is okay)
Y

/* I get a warning about the arc attributes not matching,
/* but it's specious because we are going to populate the attributes down below...


/* ************************************************************************
/* Copy the "label" point fc from "taxlotbk"
/* Since there is no label fc in "taxlot" the warning about attributes not matching is specious.

ec taxlotbk                 /* Used to read SOURCE directly here, not sure why it matters
ef label  

sel taxmaptype = 31          /* BRINGING TAXLOT LABELS FROM TAXLOT COVER
put taxlot                   /* TO ENSURE WE GET MAP NUMBER & TAXLOT KEY INFO RIGHT
/* "...TAXLOT already exists, do you want to append?"
/* The coverage exists, but there is no label fc until this runs
Y

/* save    DONT save here we don't want to modify the input file tmptaxmap1
q

clean taxlot # 10 .05


/* Recreate taxlot key - IN THE POLK KEY THE TAXLOT IS RIGHT JUSTIFIED... 

&data arc info 
arc
SEL TAXLOT.PAT 
CONCAT MAPTAXLOT FROM TOWN,RANGE,SECTION,QTR,TAXLOT               /* Polk Taxlot key
Q STOP
&END 

/***********  Dean made following update so maptaxlot is consistent with ORCATS - Dean 10/17 *************************/ 
arcedit

ec taxlot 
ef label  

sel TOWN < 10                         /* GET RID OF LEADING BLANK IN TOWNSHIP < 10 
&if [show number select] > 0 &then &do
  CHANGE MAPTAXLOT C/ // 
&end
 
sel all                               /* GET RID OF LEADING BLANK IN ALL RANGES 
CHANGE MAPTAXLOT C/ // 

sel SECTION = 0 
&if [show number select] > 0 &then &do
  CHANGE MAPTAXLOT C/ 0// 
&end

SAVE
Q




/* 5. Calculate the values for taxlot label fc

arcedit 
ec taxlot
ef label

sel all
change TAXLOT C/ //G                           /* LEFT JUSTIFY TAXLOT NUMBERS 

save
q


&DATA ARC INFO
ARC
SEL TAXLOT.AAT                                  /* CALC LINEWORK 
MOVE ANGLE TO DIRECTION
CALC LINETYPE = TAXMAPTYPE 
CALC AUTODATE = AUTO-DATE
MOVE AUTO-WHO TO AUTOWHO 
CALC RELIACODE = AUTO-REL
MOVE AUTO-METHOD TO AUTOMETHOD 
MOVE 'OTHER' TO SOURCETYPE 
MOVE 'OLDTAXMAPS' TO SOURCE 
/* RES TAXMAPTYPE = 62                         /* Dean added this recently, don't know why
/* CALC LINETYPE = 32                          /* Dean added this recently, don't know why

SEL TAXLOT.PAT                                  /* CALC POLYGONS 

CALC COUNTY = 4                                /* Assign County number! 
MOVE 'N' TO TOWNDIR 
MOVE 'W' TO RANGEDIR 
CALC TOWNPART = 0 
CALC RANGEPART = 0 
CALC SECNUMBER = SECTION 
MOVE '---' TO MAPSUFTYPE 
MOVE '000' TO MAPSUFNUM
MOVE '--' TO ANOMALY
/* CALC MAPACRES = AREA / 43560    /* Sometimes fails with overflow error
CALC MAPACRES = 0                  /* FIXME: WE NEED TO FIX THIS EVENTUALLY!!!!!!!!!!!!!!!!!!!!!!!!!!!
CALC TAXLOTACRE = LEGACRES1 

ALTER QTR                                         /* PLAY WITH QUARTER QUARTER SECITON NUMBERS
OQTR,,,,,,,,,,,,                         
ALTER STQTR
QTR,,,,,,,,,,,,,

RED                                               /* CHECK IS NOT ALWAYS AT THIS COLUMN
43,CQTR,1,1,C
44,CQTRQTR,1,1,C 

MOVE CQTR TO QTR
MOVE CQTRQTR TO QTRQTR 

CALC AUTODATE = AUTO-DATE
MOVE AUTO-METHOD TO AUTOMETHOD 
CALC RELIACODE = 0 
MOVE AUTO-WHO TO AUTOWHO 

                                                       /* GET ORMAP NUMBER STUFF RIGHT

MOVE MAPNUM TO MAPNUMBER 
CONCACT ORMAPNUM FROM '04',TOWN,'.00','S',RANGE,'.00','W',SECTION,QTR,QTRQTR,'--0000'
CONCAT ORTAXLOT FROM ORMAPNUM,TAXLOT

Q STOP
&END 



arcedit
ec taxlot 
ef label 
sel all
change ORMAPNUM C/ /0/G 
change ORTAXLOT C/ /0/G 
save
q 

dropitem taxlot.pat taxlot.pat 
R
MAPNUM
SECTION
OQTR
TAXMAPKEY
LEGACRES1
TAXMAPTYPE
AUTO-WHO
AUTO-METHOD
AUTO-DATE
CQTR
CQTRQTR
[UNQUOTE ' '] 
Y
Y

dropitem taxlot.aat taxlot.aat 
TAXMAPTYPE
AUTO-WHO
AUTO-METHOD
AUTO-DATE
AUTO-REL 
ANGLE
[UNQUOTE ' '] 
Y
Y

/* Copy the points for taxlot labels to taxlotpt

arcedit 

ec taxlot
ef label 
sel all
put taxlotpt 

/* save   We're not changing anything in taxlot here so no save
quit 

/* I don't know why the arcedit step did not already do this... 
/* Yes actually I just checked and it appears there are already attributes
/* so I commented this out, pretty sure it does nothing at all.
/*
/* build taxlotpt point 

kill taxlotbk all

&RETURN
