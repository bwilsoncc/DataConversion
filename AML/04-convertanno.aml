/* Creates annotation and sets scale 
/* Input:
/*    taxbound
/*    taxmap
/*    annostd.lut - a table telling what font, fontsize, bold, italic to use for each level and scale
/* Output:
/*    taxmap    annotation.igds
/*    taxseemap annotation.igds
&WATCH 04-convertanno.wat &COMMANDS
&ECHO &ON

&if ^ [exists taxbound -cover] &then &do 
  &type No Taxbound ; did you run 01-MakeMapIndex.aml ??
  &return ; &return 
&end 

&if ^ [exists taxmap -cover] &then &do 
  &type No taxmap ; did you run 01-MakeMapIndex.aml ??
  &return ; &return 
&end 

&if [exists taxlotan -cover] &then kill taxlotan all
&if [exists taxcodan -cover] &then kill taxcodan all

&if [exists taxseemap -cover] &then kill taxseemap all 
&if [exists tbigds -cover] &then kill tbigds all 
&if [exists igds -cover] &then kill igds all 
&if [exists taxmapbk -cover] &then kill taxmapbk all 
&S X = [DELETE TAXMAPBK.ACODE -INFO] 
&S X = [DELETE TAXMAPBK.TATATIGDS -INFO]
&S X = [DELETE ANNOSTD.LUT -INFO]

copy taxmap taxmapbk                 /* just in case you mess up -- normally deleted in last step

/*COPYINFO P:\taxmaped\apps\ConvertTwnToGeo\AnnoGenCov\info!ARC!ANNOSTD.LUT  ANNOSTD.LUT 
COPYINFO C:\GeoModel\Clatsop\AnnoGenCov\info!ARC!ANNOSTD.LUT  ANNOSTD.LUT 



ADDITEM TAXMAP.TATIGDS TAXMAP.TATIGDS AUTOWHO,30,30,C 
ADDITEM TAXMAP.TATIGDS TAXMAP.TATIGDS AUTOMETHOD,3,3,C 
ADDITEM TAXMAP.TATIGDS TAXMAP.TATIGDS AUTODATE,8,10,D 
ADDITEM TAXMAP.TATIGDS TAXMAP.TATIGDS STDSIZE,4,5,B 


/* 2. Save annotation for seemaps and save map assignments 

/* Current way Dean does it -- there is a separate annotation.seemap subclass.
/* We don't have that so we do it the old way.
/* &IF [EXIST TEMPANNO -COVER] &THEN KILL TEMPANNO ALL 
/* 
/* arcedit
/* ec taxmap
/* ef anno.igds 
/* sel $level > 999
/* &IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
/*   put tempanno 
/*   delete
/*   ef anno.seemap 
/*   get tempanno igds 
/* &end 
/* save
/* q
/* arcedit
/* ec taxmap
/* ef anno.igds
/* sel all
/* calc AUTOWHO = 'MCH' 
/* calc AUTODATE = $TODAY 
/* calc AUTOMETHOD =  'CNV'
/* calc maplevel = $level 
/* calc mapsize = $size
/* save
/* q 

arcedit
ec taxmap
ef anno.igds 
sel $level > 1000 
put taxseemap 
delete 
sel all
calc AUTOWHO = 'MCH' 
calc AUTODATE = $TODAY 
calc AUTOMETHOD =  'CNV'
calc maplevel = $level 
calc mapsize = $size
save
q 

/* 3. Build point coverage for annotation and overlay it with map boundary 
/*     using generate and intersect 

&data arc info 
arc
sel TAXMAP.TATIGDS
ALTER IGDS-ID 
,10,,,,,,,,,,,,,
CALC IGDS-ID = IGDS# 
OUTPUT ../GENANNO.TXT INIT
CALC $COMMA-SWITCH = -1 
DIS IGDS-ID,X,Y PRINT 
DIS 'end' print 
q stop
&end 

IDEDIT TAXMAP ANNO.IGDS 

generate IGDS
INPUT GENANNO.TXT 
POINTS 
Q

build IGDS point
intersect igds taxbound tbigds point 

/* 4. GET SCALE FOR BOTH MAPS AND DETAILS 

&DATA ARC INFO
ARC
SEL TBIGDS.PAT 
ALTER IGDS-ID
,10,,,,,,,,,,,
SORT IGDS-ID 
SEL TAXMAP.TATIGDS 
RELATE TBIGDS.PAT BY IGDS-ID WITH ORDER
CALC $NM = 1 
MOVE $1MAPNUMBER TO MAPNUMBER 
CALC MAPSCALE = $1MAPSCALE 
Q STOP
&END 

/* 7. CALC FEATURE TYPES TO STANDARD 

arcedit
EC TAXMAP 
EF ANNO.IGDS
SEL $LEVEL = 56
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
  CALC $LEVEL = 58 
&END
SEL $LEVEL = 20 
RES $TEXT CN 'P.' OR $TEXT CN '+' OR $TEXT CN 'PC' OR $TEXT CN 'EQUATION' or $TEXT CN 'STA. ' or $TEXT CN 'PT' 
/*RES $TEXT CN 'P.T.' OR $TEXT CN 'P.C.' OR $TEXT CN 'P.R.C.' OR $TEXT CN 'PT' OR $TEXT CN '+'  OR $TEXT CN 'ST ' OR $TEXT CN 'S.T' or $TEXT CN 'STATION'
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
   NSEL 
   RES $LEVEL = 20 
   &IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
     CALC $LEVEL = 36	
   &END 
&END  

/* All unassigned annotation is going into ref notes regardless of size 

sel $level = 10 or $level = 13 or $level = 16 or $level = 19 or $level = 20 or $level = 23 or $level = 25 or $level = 27 or $level = 34 or $level = 36 or $level = 37 
asel $level = 38 or $level = 39 or $level = 41 or $level = 42  or $level = 43 or $level = 45 or $level = 46 or $level = 47 or $level = 54 or $level = 58 or $level = 62
res $level < 1000 
nsel 

&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
	CALC $LEVEL = 36
&END 

SEL ALL
/* CALC $SIZE = 0
CALC $SYMBOL = $LEVEL 
CALC LEVEL = $LEVEL 

SEL $LEVEL = 34 or $level = 41 
CALC $SIZE = $SIZE * 1.4                                /* Dean Added Jan 25 2018 

SAVE 
Q

/* CALC NEW STANDARD SIZES AS SET BY Tom from Christi --- Dean loaded to $SIZE - Jan 2018 


&DATA ARC INFO
ARC
CALC $NM = 1
SEL TAXMAP.TATIGDS
RELATE ANNOSTD.LUT BY LEVEL WITH ORDER 
ASEL
RES MAPSCALE = 100
CALC STDSIZE = $1SIZE100
ASEL
RES MAPSCALE = 200 
CALC STDSIZE = $1SIZE200
ASEL
RES MAPSCALE = 400 
CALC STDSIZE = $1SIZE400
ASEL
RES MAPSCALE = 2000 
CALC STDSIZE = $1SIZE2000

ASEL
RES MAPSCALE = 100 AND LEVEL = 10 AND MAPSIZE <> 20
CALC STDSIZE = $1SIZE100 * MAPSIZE / 20 
ASEL
RES MAPSCALE = 200 AND LEVEL = 10 AND MAPSIZE <> 28
CALC STDSIZE = $1SIZE200  * MAPSIZE / 28 
ASEL
RES MAPSCALE = 400 AND LEVEL = 10 AND MAPSIZE <> 40
CALC STDSIZE = $1SIZE400  * MAPSIZE / 40 
ASEL
RES MAPSCALE = 2000 AND LEVEL = 10 AND MAPSIZE <> 200
CALC STDSIZE = $1SIZE2000  * MAPSIZE / 200 

ASEL
RES MAPSCALE = 100 AND LEVEL = 41 AND MAPSIZE <> 24
CALC STDSIZE = $1SIZE100  * MAPSIZE / 24 
ASEL
RES MAPSCALE = 200 AND LEVEL = 41 AND MAPSIZE <> 48
CALC STDSIZE = $1SIZE200 * MAPSIZE / 48 
ASEL
RES MAPSCALE = 400 AND LEVEL = 41 AND MAPSIZE <> 96
CALC STDSIZE = $1SIZE400 * MAPSIZE / 96 
ASEL
RES MAPSCALE = 2000 AND LEVEL = 41 AND MAPSIZE <> 480
CALC STDSIZE = $1SIZE2000 * MAPSIZE / 480 

ASEL
RES MAPSCALE = 100 AND LEVEL = 54 AND ( MAPSIZE < 49 or MAPSIZE > 51 ) 
CALC STDSIZE = $1SIZE100  * MAPSIZE / 50
ASEL
RES MAPSCALE = 200 AND LEVEL = 54 AND ( MAPSIZE < 99 or MAPSIZE > 101 ) 
CALC STDSIZE= $1SIZE200 * MAPSIZE / 100 
ASEL
RES MAPSCALE = 400 AND LEVEL = 54 AND ( MAPSIZE < 199 or MAPSIZE > 201 ) 
CALC STDSIZE = $1SIZE400 * MAPSIZE / 200
ASEL
RES MAPSCALE = 2000 AND LEVEL = 54 AND ( MAPSIZE < 999 or MAPSIZE > 1001 ) 
CALC STDSIZE = $1SIZE2000 * MAPSIZE / 1000

Q STOP
&END 

arcedit
ec taxmap
ef anno.igds
sel all
/* calc $size = stdsize 
save
q
ADDTEXT TAXMAP IGDS 

/* Create taxlot and taxcode anno - Dean Nov 2017 to remove linked anno 
/* Update so have anno levels for each scale for both 

/* make Taxlot Anno 

reselect taxmap taxlotan anno.igds      
RES level = 37 and ( text cn 'L' or text nc 'C' or text nc 'A' or text nc 'M' or text nc 'U' )
[unquote ' ']
n
n

/* Make Taxcode Anno 

reselect taxmap taxcodan anno.igds      /* make coverage 
res level = 54 
[unquote ' ']
n
n


/* Delete both from Taxmap Anno 

ARCEDIT 
EC taxmap
EF anno.igds 
sel $level = 37 and ( $text cn 'L' or $text nc 'C' or $text nc 'A' or $text nc 'M' or $text nc 'U' )
DELETE 
sel $level = 54 
DELETE 
SAVE
Q


/* edit taxcode and taxlot anno and assign new levels based on scale - Dean Jan 2018 

arcedit 
ec taxlotan
ef anno.igds
sel mapscale = 100 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO  
  calc $level = 0
  calc $symbol = 0
&end 
sel mapscale = 200 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
  calc $level = 1
  calc $symbol = 1 
&end
sel mapscale = 400 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
calc $level = 2
calc $symbol = 2 
&end
sel mapscale = 2000 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
calc $level = 3
calc $symbol = 3 
&end
save 
ec taxcodan 
ef anno.igds
sel mapscale = 100 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
calc $level = 0
calc $symbol = 0 
&end 
sel mapscale = 200 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO  
calc $level = 1
calc $symbol = 1 
&end
sel mapscale = 400 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
calc $level = 2
calc $symbol = 2 
&end
sel mapscale = 2000 
&IF [SHOW NUMBER SELECT] > 0 &THEN &DO 
calc $level = 3
calc $symbol = 3 
&end
save
Q

addtext TAXLOTAN IGDS 
addtext TAXCODAN IGDS


/* KILL TEMPS 

&if [exists tbigds -cover] &then kill tbigds all 
&if [exists igds -cover] &then kill igds all 

&RETURN
