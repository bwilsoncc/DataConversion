/* 
/*  Dean 10/24/05

/* Input:
/*   taxmap coverage from the source workspace
/* Output:
/*   taxmap    basically a copy of the source taxmap coverage
/*   taxbound  map and detail map polygons extracted from taxmap
/*   mapindex  taxmap with a bunch of fields dropped and MAPSCALE added and filled in

/********************************************************************************
&args source
&WATCH 01-MakeMapIndex.wat
&ECHO &ON 
&term 9999 
DISPLAY 9999 1

/***************************************************************************
/* 1. delete coverages you will create and check if inputs are present 

&if [exists taxtemp -cover] &then kill taxtemp all
&if [exists taxbound -cover] &then kill taxbound all
&if [exists mapindex -cover] &then kill mapindex all 
&if [exists taxmap -cover] &then kill taxmap all

copy %source%\taxmap taxmap 

&S X = [DELETE TAXMAP.ACODE -INFO] 
&S X = [DELETE TAXMAP.TATATIGDS -INFO] 

/* 1.5 - add taxmap annotation attributes so you do not have to do it later 

addtext taxmap igds 
additem taxmap.tatigds taxmap.tatigds maplevel 3 3 i 
additem taxmap.tatigds taxmap.tatigds mapsize 8 8 n 2 
additem taxmap.tatigds taxmap.tatigds mapscale 5 5 i
additem taxmap.tatigds taxmap.tatigds mapnumber 20 20 c 


/* 2. make taxbound and taxdetail polygons => taxbound coverage

PRECISION DOUBLE DOUBLE 

arcedit 

EC TAXMAP 
ef arc
sel taxmaptype = 51 or taxmaptype = 52
put taxbound
ef label
sel taxmaptype = 51 or taxmaptype = 52
put taxbound

save 
q 

clean taxbound # 10 .1

/* 3. Add fields 

ADDITEM TAXBOUND.PAT TAXBOUND.PAT  MAPSCALE 5 5 I 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  MAPNUMBER 20 20 C 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  PAGENUMBER 3 3 I
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  ORMAPNUM 24 24 C 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  MAPSUFTYPE 1 1 C 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  MAPSUFNUM 4 4 I 

ADDITEM TAXBOUND.PAT TAXBOUND.PAT  RELIACODE 2 2 I 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  AUTODATE 8 10 D  
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  AUTOMETHOD 3 3 C  
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  AUTOWHO 30 30 C 
ADDITEM TAXBOUND.PAT TAXBOUND.PAT  CITYNAME 50 50 C 

&DATA ARC INFO
ARC
SEL TAXBOUND.PAT 
RED
45,MQ,1,1,C 
46,MQQ,1,1,C 

Q STOP
&END 

/* 4. CALCULATE MAP NUMBER INFORMATION
/* Used to be called "3. CALCULATE SCALE FOR TAXDETAIL"
/* which seems more accurate to me.

&DATA ARC INFO
ARC
SEL TAXBOUND.PAT 
MOVE '---' TO MAPSUFTYPE /* Used to be '0'
MOVE '000' TO MAPSUFNUM 
CALC PAGENUMBER = 1

CALC AUTODATE = AUTO-DATE
MOVE AUTO-METHOD TO AUTOMETHOD 
CALC RELIACODE = 0 
MOVE AUTO-WHO TO AUTOWHO 

MOVE MAPNUM TO MAPNUMBER
CONCACT ORMAPNUM FROM '04',TOWN,'.00','S0',RANGE,'.00','W',SECTION,QTR,'--0000'

/* Set the scale based on section, qsection, qqsection
CALC MAPSCALE = 2000 /* 1:24000
RES SECTION > 0 
CALC MAPSCALE = 400  /*  1:4800
RES QTR NC '00' 
CALC MAPSCALE = 200  /*  1:2400
RES QTR NC '0'
CALC MAPSCALE = 100  /*  1:1200

Q STOP
&END 

/* +--------- Brian pasted this section in from ConvertAMLS
/* |
/* 4. CALCULATE DETAIL AND ORMAPNUM FOR DETAILS 
/* NOTE.... THIS IS STRANGE SECTION. CLATSOP DOES NOT HAVE DETAILS OR SUFFIX MAPS 
/* NUMBERED. SO I ADDED A SUFFIX NUMBER TO THE FILE AND NOW I HAVE TO GO THRU MAP 
/* NUMBER BY MAP NUMBER AND ASSIGN THEM A NUMBER. I DID THIS USING AN INFO PROGRMA 
/* 

&DATA ARC INFO 
ARC
SEL TAXBOUND.PAT 
ERASE CALCSUFNUM
Y
PROGRAM CALCSUFNUM
FORMAT $CHR10,10,10,C
FORMAT $NUM1,4,4,I
SEL TAXBOUND.PAT
RES DETAILSCALE > 0 
CALC MAPSCALE = DETAILSCALE 
MOVE 'D' TO MAPSUFTYPE 
SORT MAPNUM
CALC $NUM1 = 1
MOVE ' ' TO $CHR10
PROGRAM SECTION 2
CALC MAPSUFNUM = 1
IF MAPNUM = $CHR10
  CALC $NUM1 = $NUM1 + 1
  CALC MAPSUFNUM = $NUM1
  CALC PAGENUMBER = MAPSUFNUM
ENDIF
IF MAPNUM NE $CHR10
  CALC $NUM1 = 1
  CALC MAPSUFNUM = 1
  CALC PAGENUMBER = MAPSUFNUM 
ENDIF
MOVE MAPNUM TO $CHR10
PROGRAM SECTION 2N
ASEL
SORT TAXBOUND#

RUN CALCSUFNUM

ASEL
RES DETAILSCALE > 0 
CONCACT ORMAPNUM FROM '040',TOWN,'.00','N',RANGE,'.00','W',SECTION,QTR,'--D',MAPSUFNUM

Q STOP
&END
/* |
/* +---------

/* 7. GET RID OF LEADING ZEROES IN ORMAPNUM 

ARCEDIT 
EC TAXBOUND
EF LABEL 
SEL ALL
CHANGE ORMAPNUM C/ /0/G 
SAVE 
Q  

/* 8. Drop un-needed numbers 

COPY TAXBOUND MAPINDEX 
DROPITEM MAPINDEX.PAT MAPINDEX.PAT 
IGDS-GGNO
MAPNUM
TOWN
RANGE
SECTION
QTR
TAXLOT
REFOREST
TAXCODE
CLOSEMETHOD
CLOSERROR
LEGACRES1
LEGACRES2
MAPACRES
AUTO-METHOD
AUTO-REL
AUTO-WHO
AUTO-DATE
GEOTRANSNO
TAXMAPTYPE
TAXMAPSYM
POINTNAME
MAPSUFTYPE
MAPSUFNUM
TAXLOTC
MQ
MQQ
[UNQUOTE ' '] 
Y
Y

/* .aat is the attribute data
/* This adds a new field to the line fc (arc)
/* called "CURRENT" and sets it to "Y"

&s x = [delete mapindex.aat -info] 
build mapindex line 
additem mapindex.aat mapindex.aat current 1 1 c 

&data arc info
arc
SEL MAPINDEX.AAT
MOVE 'Y' TO CURRENT

Q STOP
&END 

&if [exists taxtemp -cover] &then kill taxtemp all

&RETURN



 

