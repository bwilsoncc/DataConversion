/* This script is IDEMPOTENT,
/* it does not change tmptaxmap1 but it creates tmptaxcode
/*
/* Args:   NONE
/*
/* Input:
/*   tmptaxmap1
/*
/* Output:
/*   tmptaxcode

&WATCH 03-taxcode.wat

/***************************************************************************

&if ^ [exists tmptaxmap1 -cover] &then &do 
 &type Missing tmptaxmap1
  &return ; &return 
&end 

&type -- 1. Delete coverages that will be created here

&do name &list tmptaxcode
  &if [exists %name% -cover] &then kill %name% all
&end

/* Set coordinate precision
PRECISION DOUBLE DOUBLE

&type -- 2. Copy selected features from taxmap to taxcode coverage
/* I think we want a line feature and a point feature
/* Then we can make polygons and annotation later

/* 51 map
/* 53 ??
/* 54 taxcode

arcedit
ec tmptaxmap1

/* Copy lines from tmptaxmap1
ef arc
/* transfercoverage tmptaxmap1
/* transferfeature arc
/* transferitems PAGENAME MAPSCALE RELIACODE AUTODATE AUTOMETHOD AUTOWHO
sel taxmaptype = 51 or taxmaptype = 53
put tmptaxcode

/* Copy labels from tmptaxmap1
ef label
/* transfercoverage tmptaxmap1
/* transferfeature label
/* transferitems PAGENAME MAPSCALE
sel taxmaptype = 54
put tmptaxcode

q

clean tmptaxcode # 10 .05

&DATA ARC INFO
ARC
SEL TMPTAXCODE.PAT
CALC TMPTAXCODE-ID = TMPTAXCODE# 
Q STOP
&END

/* Update USERID, not sure what for.
IDEDIT TMPTAXCODE 

&type -- 3. Add attributes to points
/* AUTO and friends are already there and populated, using TRANSFERITEMS above.

additem tmptaxcode.pat tmptaxcode.pat county 3 3 i 
additem tmptaxcode.pat tmptaxcode.pat otaxcode 8 8 c 
additem tmptaxcode.pat tmptaxcode.pat source 20 20 c 
additem tmptaxcode.pat tmptaxcode.pat YearCreated 8 10 d 
/* additem tmptaxcode.pat tmptaxcode.pat ReliaCode 2 2 i 
/* additem tmptaxcode.pat tmptaxcode.pat Autodate 8 10 d 
/* additem tmptaxcode.pat tmptaxcode.pat AutoMethod 3 3 c 
/* additem tmptaxcode.pat tmptaxcode.pat AutoWho 30 30 c 
additem tmptaxcode.pat tmptaxcode.pat TMPTAXCODEID 8 8 I

&type -- 4. Calc items 

&DATA ARC INFO 
ARC
SEL TMPTAXCODE.PAT 
MOVE TAXCODE TO OTAXCODE
ALTER TAXCODE 
CTAXCODE,,,,,,,,,,,
ALTER OTAXCODE 
TAXCODE,,,,,,,,,,,, 
CALC COUNTY = 4
MOVE 'OLD TAXMAPS' TO SOURCE 
CALC YEARCREATED = $TODAY  
REM  CALC RELIACODE = 0 
REM  CALC AUTODATE = $TODAY
REM  MOVE 'CNV' to AUTOMETHOD 
REM  MOVE 'MCH' TO AUTOWHO
REM "MOVE" won't work here because TMPTAXCODE-ID is type B
CALC TMPTAXCODEID = TMPTAXCODE-ID
Q STOP
&END 

/* 5. Drop un-needed items 

dropitem tmptaxcode.pat tmptaxcode.pat CTAXCODE
/* dropitem tmptaxcode.pat tmptaxcode.pat TAXMAPTYPE
dropitem tmptaxcode.pat tmptaxcode.pat TMPTAXCODE-ID

/* 6. MAKE TAXCODE LINES 

build tmptaxcode line 

additem tmptaxcode.aat tmptaxcode.aat CURRENTLINE 1 1 C 

&DATA ARC INFO 
ARC
SEL TMPTAXCODE.AAT 
MOVE 'Y' TO CURRENTLINE
Q STOP
&END 

&type ---- That's all! ----
&return
