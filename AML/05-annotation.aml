/* Generate the coverages for annotation
/*
/* Args:
/*   NONE
/* Input:
/*   tmptaxmap2 anno.igds
/*
/* Output:
/*   annotation in tmpbearingan tmptaxlotan tmptaxcodan tmpseemapan tmptaxmapan lotsonlyan

&WATCH 05-annotation.wat

/* I guess .arc is not getting run when running from Python?
/* hence set the environment variables here:
&s PROJECTHOME = C:/GeoModel/Clatsop
&AMLPATH %PROJECTHOME%/AML
&type PROJECTHOME = %PROJECTHOME%
&type AMLPATH = [show &amlpath]

/* Hack to work around the fact that I can't seem to pass $SYMBOL
/* as an arg, so copy it to SYMBOL and use that

arcedit
ec tmptaxmap2
ef anno.igds
sel all
calc SYMBOL = $SYMBOL
save
q


/* ------------------------------------------------------------------------
&type 1 TAXLOT ANNOTATION
/* NB "taxcodanno" is a template
&r makeanno taxcodanno tmptaxlotan SYMBOL = 37

&if [exists tmptaxlotan -cover] &then &do
  arcedit
  ec tmptaxlotan
  ef anno.igds
  sel all
  calc $SYMBOL = $LEVEL
  save
  q
&end

/* ------------------------------------------------------------------------
&type 2 BEARING AND DISTANCE
/* NB "taxcodanno" is a template
&r makeanno taxcodanno tmpbearingan SYMBOL = 34
&if [exists tmpbearingan -cover] &then &do
  arcedit
  ec tmpbearingan
  ef anno.igds
  sel all
  calc $SYMBOL = $LEVEL
  &type Fixing font size for bearing/dimension
  /* This was 1.4, I bumped it up 2018/03/23
  calc $SIZE = $SIZE * 1.5
  save
  q
&end

/* ------------------------------------------------------------------------
&type 3 TAXCODE ANNOTATION
/* NB "taxcodanno" is a template
&r makeanno taxcodanno tmptaxcodan SYMBOL = 54
&if [exists tmptaxcodan -cover] &then &do
  arcedit
  ec tmptaxcodan
  ef anno.igds
  sel all
  calc $SYMBOL = $LEVEL
  save
  q
&end

/* ------------------------------------------------------------------------
&type 4 "SEE MAP" ANNOTATION
/* NB "taxcodanno" is a template
&r makeanno taxcodanno tmpseemapan SYMBOL = 62
&if [exists tmpseemap -cover] &then &do
  arcedit
  ec tmpseemapan
  ef anno.igds
  sel all
  calc $SYMBOL = $LEVEL
/* &type Adjust SEE MAPS font sizes
/* SEL MAPSCALE < 100
/* &IF [SHOW NUMBER SELECT] > 0 &THEN  CALC $SIZE = 14
/* SEL MAPSCALE = 100
/* &IF [SHOW NUMBER SELECT] > 0 &THEN  CALC $SIZE = 16
/* SEL MAPSCALE = 200
/* &IF [SHOW NUMBER SELECT] > 0 &THEN  CALC $SIZE = 33
/* SEL MAPSCALE = 400
/* &IF [SHOW NUMBER SELECT] > 0 &THEN  CALC $SIZE = 66
/* SEL MAPSCALE = 2000
/* /* This used to be 315 which seemed absurdly big to me. 2018-03-23
/* &IF [SHOW NUMBER SELECT] > 0 &THEN  CALC $SIZE = 180
  save
  q
&end

/* ------------------------------------------------------------------------
&type 5 TAXMAP ANNOTATION
/* (Catch everything else...)
&r makeanno taxmapancc tmptaxmapan SYMBOL <> 34 AND SYMBOL <> 37 AND SYMBOL <> 54 AND SYMBOL <> 62
&if [exists tmptaxmapan -cover] &then &do
  /* DON'T change $SYMBOL on this annotation, we want to use it later!
  /* BUT rewrite things I don't understand into REF NOTE so they don't disappear
  arcedit
  ec tmptaxmapan
  ef anno.igds
  SEL $SYMBOL IN {10,13,16,19,20,23,25,27,36,38,39,41,42,43,45,46,47,56,58}
  NSEL
  &if [show number select] > 0 &then CALC $SYMBOL = 36
  /* Is this needed??? It does appear to be working.
  &type Fixing font size for subdivision
  SEL $SYMBOL = 41
  calc $SIZE = $SIZE * 1.4
  SAVE
  Q
&end

&return
