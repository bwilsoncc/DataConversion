/* See FIXME, we're putting 0 in acres due to an ARC overflow error
/* I think I can either ignore that or fix it in python later.

/* This script is IDEMPOTENT,
/* it does not change inputs,
/* it creates taxlot and taxlotpt

/*  Dean 10/24/05
/********************************************************************************
/* Args:
/*   source     workspace containing read-only source data
/* 
/* Input:
/*   %source%/taxlot
/*   tmptaxmap1
/* 
/* Output:
/*   tmptaxlot   -- line and label pt feature classes
/*   tmptaxlotpt -- will be used to define attributes in final polygons
/*                  this will just be a copy of tmptaxlot.label
/*
&args source
/* For example, &r 03-maketaxlot.aml %PROJECTHOME%/Source/t4-6

&WATCH 03-taxlot.wat

/***************************************************************************

&if ^ [exists tmptaxmap1 -cover] &then &do 
 &type No tmptaxmap1, did you run 01-MakeMapIndex.aml??
  &return ; &return 
&end 

&type -- 1. Delete coverages that will be created

&do name &list tmptaxlot, tmptaxlotpt
  &if [exists %name% -cover] &then kill %name% all
&end

&type -- 2. Create taxlot coverage 

PRECISION DOUBLE DOUBLE 

create tmptaxlot 
build tmptaxlot line
build tmptaxlot point 
tolerance tmptaxlot FUZZY .01 

&type -- 3. Add point attribute fields

additem tmptaxlot.pat tmptaxlot.pat COUNTY,2,2,I 
additem tmptaxlot.pat tmptaxlot.pat TOWN,2,2,I 
additem tmptaxlot.pat tmptaxlot.pat TOWNPART, 3,3,N,2
additem tmptaxlot.pat tmptaxlot.pat TOWNDIR,1,1,c 
additem tmptaxlot.pat tmptaxlot.pat RANGE,2,2,I                          
additem tmptaxlot.pat tmptaxlot.pat RANGEPART,3,3,n,2              
additem tmptaxlot.pat tmptaxlot.pat RANGEDIR ,1,1,c  
additem tmptaxlot.pat tmptaxlot.pat SECTION,2,2,I             
additem tmptaxlot.pat tmptaxlot.pat SECNUMBER,2,2,I  
additem tmptaxlot.pat tmptaxlot.pat QTR, 2,2 ,C             
additem tmptaxlot.pat tmptaxlot.pat STQTR,1,1,c                            
additem tmptaxlot.pat tmptaxlot.pat QTRQTR,1,1,c                    
additem tmptaxlot.pat tmptaxlot.pat ANOMALY,2,2,c                
additem tmptaxlot.pat tmptaxlot.pat MAPSUFTYPE ,1,1,c         
additem tmptaxlot.pat tmptaxlot.pat MAPSUFNUM,4,4,I            
additem tmptaxlot.pat tmptaxlot.pat MAPNUMBER,20,20,c
additem tmptaxlot.pat tmptaxlot.pat MAPNUM,10,10,C 
additem tmptaxlot.pat tmptaxlot.pat ORMAPNUM,24,24,c        
additem tmptaxlot.pat tmptaxlot.pat TAXLOT,5,5,c                       
additem tmptaxlot.pat tmptaxlot.pat SPECIALINT,1,1,c               
additem tmptaxlot.pat tmptaxlot.pat MAPTAXLOT ,25,25,c          
additem tmptaxlot.pat tmptaxlot.pat ORTAXLOT,29,29,c  
additem tmptaxlot.pat tmptaxlot.pat TAXLOTFEET,8,8,I 
additem tmptaxlot.pat tmptaxlot.pat TAXLOTACRE,8,8,N,2 
additem tmptaxlot.pat tmptaxlot.pat MAPACRES,8,8,N,2 
additem tmptaxlot.pat tmptaxlot.pat RELIACODE,2,2,I 
additem tmptaxlot.pat tmptaxlot.pat AUTODATE,8,10,D
additem tmptaxlot.pat tmptaxlot.pat AUTOMETHOD,3,3,C 
additem tmptaxlot.pat tmptaxlot.pat AUTOWHO,30,30,C 
additem tmptaxlot.pat tmptaxlot.pat TAXMAPKEY,12,12,C 
additem tmptaxlot.pat tmptaxlot.pat LEGACRES1 9 9 N 2 

additem tmptaxlot.pat tmptaxlot.pat TAXMAPTYPE,3,3,I 
additem tmptaxlot.pat tmptaxlot.pat AUTO-WHO,3,3,C 
additem tmptaxlot.pat tmptaxlot.pat AUTO-METHOD,3,3,C 
additem tmptaxlot.pat tmptaxlot.pat AUTO-DATE,8,10,D

&type -- Add taxlot line attributes

additem tmptaxlot.aat tmptaxlot.aat TAXMAPTYPE,3,3,I 
additem tmptaxlot.aat tmptaxlot.aat AUTO-WHO,3,3,C 
additem tmptaxlot.aat tmptaxlot.aat AUTO-METHOD,3,3,C 
additem tmptaxlot.aat tmptaxlot.aat AUTO-DATE,8,10,D 
additem tmptaxlot.aat tmptaxlot.aat AUTO-REL,1,1,I 
 
additem tmptaxlot.aat tmptaxlot.aat LINETYPE,3,3,I
additem tmptaxlot.aat tmptaxlot.aat DIRECTION,12,12,C 
additem tmptaxlot.aat tmptaxlot.aat DISTANCE,10,10,C
additem tmptaxlot.aat tmptaxlot.aat ANGLE,10,10,C
additem tmptaxlot.aat tmptaxlot.aat DELTA,10,10,C
additem tmptaxlot.aat tmptaxlot.aat RADIUS,10,10,C 
additem tmptaxlot.aat tmptaxlot.aat RADIUS2,10,10,C
additem tmptaxlot.aat tmptaxlot.aat TANGENT,10,10,C 
additem tmptaxlot.aat tmptaxlot.aat TANGENT2,10,10,C
additem tmptaxlot.aat tmptaxlot.aat ARCLENGTH,10,10,C 
additem tmptaxlot.aat tmptaxlot.aat SIDE,1,1,C
additem tmptaxlot.aat tmptaxlot.aat SOURCE,20,20,C
additem tmptaxlot.aat tmptaxlot.aat SOURCETYPE,20,20,C
additem tmptaxlot.aat tmptaxlot.aat RELIACODE,2,2,I 
additem tmptaxlot.aat tmptaxlot.aat AUTODATE,8,10,D
additem tmptaxlot.aat tmptaxlot.aat AUTOMETHOD,3,3,C 
additem tmptaxlot.aat tmptaxlot.aat AUTOWHO,30,30,C 

/* At this point if I do "dir feat tmptaxlot" it shows
/* point, arc, node but no polygon

&type -- 4. Calc values 

/* 8  road
/* 14 rxr
/* 31 taxlot labels (apparently)
/* 32 parcel
/* 51 map boundary

arcedit

ec tmptaxmap1
ef arc

/* Select the features from tmptaxmap1 that will be copied into taxlot lines
/* I don't know why taxmaptype 51 is in here, it seems wrong to me
sel taxmaptype = 8 or taxmaptype = 14 or taxmaptype = 32 or taxmaptype = 51

put tmptaxlot
/* Coverage already exists. Do you want to append?  (we just created it up above so this is okay)
Y
/* I get a warning about the arc attributes not matching,
/* but it's specious because we are going to populate the attributes down below...

/* ************************************************************************
/* Copy the "label" point fc
/* Since there is no label fc in "tmptaxlot" the warning about attributes not matching is specious.

ec %source%/taxlot
ef label

sel taxmaptype = 31          /* BRINGING TAXLOT LABELS FROM TAXLOT COVER
put tmptaxlot                   /* TO ENSURE WE GET MAP NUMBER & TAXLOT KEY INFO RIGHT
/* "...TAXLOT already exists, do you want to append?"
/* The coverage exists, but there is no label fc until this runs
Y

q

/* "CLEAN" builds the polygons, that should be OBVIOUS to ANYONE!!!
/* POINT is gone now we have POLYGON

clean tmptaxlot # 10 .05

/* Recreate taxlot key - IN THE POLK KEY THE TAXLOT IS RIGHT JUSTIFIED... 

&data arc info 
arc
SEL TMPTAXLOT.PAT 
CONCAT MAPTAXLOT FROM TOWN,RANGE,SECTION,QTR,TAXLOT               /* Taxlot key
Q STOP
&END 

/*****  Dean made following update so maptaxlot is consistent with ORCATS - Dean 10/17 ******** 
arcedit
ec tmptaxlot 
ef label  

sel TOWN < 10                         /* GET RID OF LEADING BLANK IN TOWNSHIP < 10 
&if [show number select] > 0 &then &do
  CHANGE MAPTAXLOT C/ // 
&end
 
sel all                               /* GET RID OF LEADING BLANK IN ALL RANGES 
CHANGE MAPTAXLOT C/ // 

sel SECTION = 0 
&if [show number select] > 0 &then &do
  CHANGE MAPTAXLOT C/ 0// 
&end

SAVE
Q

&type -- 5. Calculate the values for taxlot label fc

arcedit 
ec tmptaxlot
ef label
sel all
change TAXLOT C/ //G                           /* Strip spaces out of field
save
q

&DATA ARC INFO
ARC
SEL TMPTAXLOT.AAT                                  /* CALC LINEWORK 
MOVE ANGLE TO DIRECTION
CALC LINETYPE = TAXMAPTYPE 
CALC AUTODATE = AUTO-DATE
MOVE AUTO-WHO TO AUTOWHO 
CALC RELIACODE = AUTO-REL
MOVE AUTO-METHOD TO AUTOMETHOD 
MOVE 'OTHER' TO SOURCETYPE 
MOVE 'OLDTAXMAPS' TO SOURCE 
REM RES TAXMAPTYPE = 62                         /* Dean added this recently, don't know why
REM CALC LINETYPE = 32                          /* Dean added this recently, don't know why

SEL TMPTAXLOT.PAT                                 /* Set polygon attributes

CALC COUNTY = 4                                /* Assign County number! 
MOVE 'N' TO TOWNDIR 
MOVE 'W' TO RANGEDIR 
CALC TOWNPART = 0 
CALC RANGEPART = 0 
CALC SECNUMBER = SECTION 
MOVE '---' TO MAPSUFTYPE 
MOVE '000' TO MAPSUFNUM
MOVE '--' TO ANOMALY
REM CALC MAPACRES = AREA / 43560    /* Sometimes fails with overflow error so I moved it to Python
CALC TAXLOTACRE = LEGACRES1 

ALTER QTR                                         /* PLAY WITH QUARTER QUARTER SECITON NUMBERS
OQTR,,,,,,,,,,,,                         
ALTER STQTR
QTR,,,,,,,,,,,,,

RED                                               /* CHECK IS NOT ALWAYS AT THIS COLUMN
43,CQTR,1,1,C
44,CQTRQTR,1,1,C 

MOVE CQTR TO QTR
MOVE CQTRQTR TO QTRQTR 

CALC AUTODATE = AUTO-DATE
MOVE AUTO-METHOD TO AUTOMETHOD 
CALC RELIACODE = 0 
MOVE AUTO-WHO TO AUTOWHO 
                                                  /* GET ORMAP NUMBER STUFF RIGHT
MOVE MAPNUM TO MAPNUMBER 
CONCACT ORMAPNUM FROM '04',TOWN,'.00','N',RANGE,'.00','W',SECTION,QTR,QTRQTR,'--0000'
CONCAT ORTAXLOT FROM ORMAPNUM,TAXLOT

Q STOP
&END 

&type -- TOWN and RANGE were blank filled above and we need zero fill

arcedit
ec tmptaxlot 
ef label 
sel all
change ORMAPNUM C/ /0/G 
change ORTAXLOT C/ /0/G 
save
q 

dropitem tmptaxlot.pat tmptaxlot.pat R MAPNUM SECTION OQTR TAXMAPKEY LEGACRES1 TAXMAPTYPE
dropitem tmptaxlot.pat tmptaxlot.pat CQTR CQTRQTR
dropitem tmptaxlot.pat tmptaxlot.pat AUTO-WHO AUTO-METHOD AUTO-DATE

dropitem tmptaxlot.aat tmptaxlot.aat TAXMAPTYPE ANGLE

/* dropitem tmptaxlot.aat tmptaxlot.aat AUTO-WHO AUTO-METHOD AUTO-DATE

&type -- Copy the points for tmptaxlot labels to tmptaxlotpt

arcedit 
ec tmptaxlot
ef label 
sel all
put tmptaxlotpt 
quit 

&type -- That's all! --
&RETURN
