/* This script is IDEMPOTENT,
/* it does not change tmptaxmap1 but it creates taxcode
/*
/* Args:   NONE
/*
/* Input:
/*   tmptaxmap1
/*
/* Output:
/*   taxcode

&WATCH 02-Maketaxcode.wat
&ECHO &ON 
&term 9999 

/***************************************************************************
/* 1. delete coverages

&if ^ [exists tmptaxmap1 -cover] &then &do 
 &type No tmptaxmap1, did you run 01-makemapindex.aml??
  &return ; &return 
&end 

&do name &list taxcode, taxcode1
  &if [exists %name% -cover] &then kill %name% all
&end

/* 2. Create taxcodes just the way mapcheck does 

PRECISION DOUBLE DOUBLE

/* 51 map
/* 53 ??
/* 54 taxcode

arcedit
ec tmptaxmap1

ef arc
sel taxmaptype = 51 or taxmaptype = 53
put taxcode1

ef label
sel taxmaptype = 54
put taxcode1

/* save     should not have to do a save here, we don't want to change tmptaxmap1
q

clean taxcode1 # 10 .05
dissolve taxcode1 taxcode taxcode

&data arc info
arc
sel TAXCODE.PAT
CALC TAXCODE-ID = TAXCODE# 
Q STOP
&END 
IDEDIT TAXCODE 

/* 3. add items to point attributes

additem taxcode.pat taxcode.pat county 3 3 i 
additem taxcode.pat taxcode.pat otaxcode 8 8 c 
additem taxcode.pat taxcode.pat source 20 20 c 
additem taxcode.pat taxcode.pat YearCreated 8 10 d 
additem taxcode.pat taxcode.pat ReliaCode 2 2 i 
additem taxcode.pat taxcode.pat Autodate 8 10 d 
additem taxcode.pat taxcode.pat AutoMethod 3 3 c 
additem taxcode.pat taxcode.pat AutoWho 30 30 c 
additem taxcode.pat taxcode.pat TAXCODEID 8 8 I

/* 4. calc items 

&data arc info 
arc
sel TAXCODE.PAT 
MOVE TAXCODE TO OTAXCODE
ALTER TAXCODE 
CTAXCODE,,,,,,,,,,,
ALTER OTAXCODE 
TAXCODE,,,,,,,,,,,, 

CALC COUNTY = 4                                      /**** ASSIGN COUNTY NUMBER!
 
MOVE 'OLD TAXMAPS' TO SOURCE 
CALC YEARCREATED = $TODAY  
CALC RELIACODE = 0 
CALC AUTODATE = $TODAY
MOVE 'CNV' to AUTOMETHOD 
MOVE 'MCH' TO AUTOWHO 

CALC TAXCODEID  = TAXCODE-ID 

Q STOP
&END 

/* 5. Drop un-needed items 

dropitem taxcode.pat taxcode.pat CTAXCODE 
dropitem taxcode.pat taxcode.pat TAXMAPTYPE 

/* 6. MAKE TAXCODE LINES 

build taxcode line 

additem taxcode.aat taxcode.aat CURRENTLINE 1 1 C 

&DATA ARC INFO 
ARC
SEL TAXCODE.AAT 
MOVE 'Y' TO CURRENTLINE
Q STOP
&END 


kill taxcode1 all

&return
