/* This script is IDEMPOTENT,
/* it does not change tmptaxmap1 but it creates taxcode
/*
/* Args:   NONE
/*
/* Input:
/*   tmptaxmap1
/*
/* Output:
/*   taxcode

&WATCH 02-Maketaxcode.wat

/***************************************************************************

&if ^ [exists tmptaxmap1 -cover] &then &do 
 &type Missing tmptaxmap1
  &return ; &return 
&end 

&type -- 1. Delete coverages that will be created here

&do name &list taxcode
  &if [exists %name% -cover] &then kill %name% all
&end

/* Set coordinate precision
PRECISION DOUBLE DOUBLE

&type -- 2. Copy selected features from taxmap to taxcode coverage
/* I think we want a line feature and a point feature
/* Then we can make polygons and annotation later

/* 51 map
/* 53 ??
/* 54 taxcode

arcedit
ec tmptaxmap1

/* Copy lines from tmptaxmap1
ef arc
/* transfercoverage tmptaxmap1
/* transferfeature arc
/* transferitems PAGENAME MAPSCALE RELIACODE AUTODATE AUTOMETHOD AUTOWHO
sel taxmaptype = 51 or taxmaptype = 53
put taxcode

/* Copy labels from tmptaxmap1
ef label
/* transfercoverage tmptaxmap1
/* transferfeature label
/* transferitems PAGENAME MAPSCALE
sel taxmaptype = 54
put taxcode

q

clean taxcode # 10 .05

/* I removed the dissolve to python, it seems to strip attributes here
/* dissolve tmptaxcode taxcode taxcode
/* kill tmptaxcode all

&DATA ARC INFO
ARC
SEL TAXCODE.PAT
CALC TAXCODE-ID = TAXCODE# 
Q STOP
&END

/* Update USERID, not sure what for.
IDEDIT TAXCODE 

&type -- 3. Add attributes to points
/* AUTO and friends are already there and populated, using TRANSFERITEMS above.

additem taxcode.pat taxcode.pat county 3 3 i 
additem taxcode.pat taxcode.pat otaxcode 8 8 c 
additem taxcode.pat taxcode.pat source 20 20 c 
additem taxcode.pat taxcode.pat YearCreated 8 10 d 
/* additem taxcode.pat taxcode.pat ReliaCode 2 2 i 
/* additem taxcode.pat taxcode.pat Autodate 8 10 d 
/* additem taxcode.pat taxcode.pat AutoMethod 3 3 c 
/* additem taxcode.pat taxcode.pat AutoWho 30 30 c 
additem taxcode.pat taxcode.pat TAXCODEID 8 8 I

&type -- 4. Calc items 

&DATA ARC INFO 
ARC
SEL TAXCODE.PAT 
MOVE TAXCODE TO OTAXCODE
ALTER TAXCODE 
CTAXCODE,,,,,,,,,,,
ALTER OTAXCODE 
TAXCODE,,,,,,,,,,,, 
CALC COUNTY = 4
MOVE 'OLD TAXMAPS' TO SOURCE 
CALC YEARCREATED = $TODAY  
REM  CALC RELIACODE = 0 
REM  CALC AUTODATE = $TODAY
REM  MOVE 'CNV' to AUTOMETHOD 
REM  MOVE 'MCH' TO AUTOWHO
REM "MOVE" won't work here because TAXCODE-ID is type B
CALC TAXCODEID = TAXCODE-ID
Q STOP
&END 

/* 5. Drop un-needed items 

dropitem taxcode.pat taxcode.pat CTAXCODE
/* dropitem taxcode.pat taxcode.pat TAXMAPTYPE
dropitem taxcode.pat taxcode.pat TAXCODE-ID

/* 6. MAKE TAXCODE LINES 

build taxcode line 

additem taxcode.aat taxcode.aat CURRENTLINE 1 1 C 

&DATA ARC INFO 
ARC
SEL TAXCODE.AAT 
MOVE 'Y' TO CURRENTLINE
Q STOP
&END 

&type ---- That's all! ----
&return
