/* --- start
&WATCH C:\GeoModel\Clatsop\Workfolder\14-ACRESanno.wat &COMMANDS
&WORKSPACE C:\GeoModel\Clatsop\Workfolder
&s source = C:\GeoModel\Clatsop\Source\t8-10
/* --- end
/* Creates the coverages needed to make linked ANNOTATION FOR ACRES
/* 
/* Inputs: 1. Taxlot Polygon coverage that contains the "taxlotkey" Mapnumber & scale?
/*         2. Taxmap point/line/anno coverage that contains taxlot anno 
/* 
/* Outputs: 1. Taxlot anno coverage with unique taxlot WHERE ACRES MATCH
/*          2. ANNO deleted from taxlot 
/* 
/* 
/* Some Customization Notes 
/* 
/*        I copy the taxlots from another workspace when it first runs 
/*        I redefine fields when calculating taxlots - this is site specific 
/*        Mapnum is defined as 10 char field 
/*        TAXMAPKEY my unique map-taxlot number 
/*        My anno class is "IGDS" - left over from initial conversion 
/* 
/* 
/*  Dean 9/24/05
/********************************************************************************

&ECHO &ON 
&term 9999 
DISPLAY 100

/***************************************************************************
/* 1. delete coverages you will create and check if inputs are present 

/***** GET A COPY OF TAXLOT TO YOUR WORK SPACE........

&if [exists taxacan -cover] &then kill taxacan all
&if [exists igds -cover] &then kill igds all
&if [exists taxlab -cover] &then kill taxlab all
&if [exists igdstaxint -cover] &then kill igdstaxint all

&if ^ [exists taxmap -cover] &then &do
  &message &popup 
  &type must have taxmap coverage 
  &message &on 
  &return 
&end 


/********************************************************************************
/* 2. create point coverage of annotation with a field called taxlot that is in the 
/* correct format. 
/*
/* 2a. create a new coverage of TAXACAN (level 38) for numbers 
             

reselect taxmap TAXACAN anno.igds      
RES level = 38
[unquote ' ']
n
n

/* 2b. create taxlot text field for matching with taxlot 

 
ADDITEM TAXACAN.TATIGDS TAXACAN.TATIGDS MAPTAXLOT 25 25 C 
ADDITEM TAXACAN.TATIGDS TAXACAN.TATIGDS OVERLAP 1 1 C 
ADDITEM TAXACAN.TATIGDS TAXACAN.TATIGDS TAXACRETXT 12 12 C 
ADDITEM TAXACAN.TATIGDS TAXACAN.TATIGDS TAXACRES 9 9 N 2 

&DATA ARC INFO
ARC
SEL TAXACAN.TATIGDS 
MOVE TEXT TO TAXACRETXT
Q STOP
&END 

ARCEDIT
EC TAXACAN
EF ANNO.IGDS 
SEL TAXACRETXT CN 'AC.'
&IF [SHOW NUMBER SELECT] > 0 &THEN CHANGE TAXACRETXT C/AC.// 

SEL TAXACRETXT CN 'AC '
&IF [SHOW NUMBER SELECT] > 0 &THEN CHANGE TAXACRETXT C/AC// 

SEL TAXACRETXT CN 'ac.'
&IF [SHOW NUMBER SELECT] > 0 &THEN CHANGE TAXACRETXT C/ac.// 

SEL TAXACRETXT CN 'Ac.'
&IF [SHOW NUMBER SELECT] > 0 &THEN CHANGE TAXACRETXT C/Ac.// 

SEL TAXACRETXT CN '\' 
&IF [SHOW NUMBER SELECT] > 0 &THEN CHANGE TAXACRETXT C/\//
SAVE 
Q 


&data arc info                            /* CONTAINS BLANKS FOR NUMERIC OVERFLOWS
arc
sel TAXACAN.TATIGDS 
RES TAXACRETXT NC 'S' AND TAXACRETXT NC 'A' AND TAXACRETXT NC 'M' AND TAXACRETXT NC 'R' 
CALC TAXACRES = TAXACRETXT 
Q STOP 
&END 

/* 2c. create a text file of coordinates and generate a point coverage 

&s x = [delete genano.txt -file] 

&data arc info
arc
SEL TAXACAN.TATIGDS
alter IGDS-ID 
,10,,,,,,
CALC IGDS-ID = IGDS# 
CALC $COMMA-SWITCH = -1 
OUTPUT ../GENANO.TXT init 
DIS IGDS-ID,X,Y print 
DIS 'END' print 
Q STOP
&END 

IDEDIT TAXACAN ANNO.IGDS 

/* 2d. generate point cover 

generate igds 
input GENANO.txt 
points
q 

build igds point
additem igds.pat igds.pat overlap 1 1 c 

/***************************************************************************
/* 3. find text in the correct polygon (OVERLAPPING AND CALC KEY TO BE CORRECT) 
/*  
/* 

/* 3a. interest the point coverage with the taxlot polygon coverage 

intersect igds taxlot igdstaxint point 

/* 3b. use INFO to link the intersected coverage with intersect. If text matches 
/* then assign the correct map number. If overlap text does not match then save 
/* for doing nearby. 

&DATA ARC INFO
ARC
SEL IGDSTAXINT.PAT 
ALTER IGDS-ID
,10,,,,,,,,
SORT IGDS-ID
sel IGDS.PAT 
ALTER IGDS-ID
,10,,,,,,,
SORT IGDS-ID 
SEL TAXACAN.TATIGDS
RELATE IGDSTAXINT.PAT BY IGDS-ID WITH ORDER
CALC $NM = 1
RES TAXACRES = $1TAXLOTACRE
MOVE $1MAPTAXLOT TO MAPTAXLOT 
RELATE IGDS.PAT BY IGDS-ID WITH ORDER 
MOVE 'Y' TO OVERLAP 
MOVE 'Y' TO $1OVERLAP 
SEL IGDS.PAT 
SORT IGDS# 
Q STOP
&END 


/*****************************************************************************
/* 4. Find text that is not overlapping 
/*
/* 4A. DELETE ALL POINTS that where annotation matched with the overap (see step 3)

ARCEDIT 
EC IGDS
EF LABEL
SEL OVERLAP CN 'Y' 
DELETE 
SAVE
Q

/* 4B. create point coverage from taxlot polygons to find nearbys 

arcedit
ec taxlot 
ef label 
sel all
put taxlab 
q

build taxlab point 

/* 4C. create distance file for points 

&S X = [DELETE NEARBYPT -INFO] 
POINTDISTANCE igds taxlab nearbypt 500

/* 4d. match overlapping points 

&DATA ARC INFO
ARC
SEL NEARBYPT 
SORT IGDS#,DISTANCE
RELATE IGDS.PAT BY IGDS# WITH ORDER
RELATE 2 TAXACAN.TATIGDS BY $1IGDS-ID 
RELATE 3 TAXLAB.PAT BY TAXLAB# WITH LINK
CALC $NM = 1
RES $2TAXACRES = $3TAXLOTACRE 
MOVE $3MAPTAXLOT TO $2MAPTAXLOT
Q STOP
&END 


/* 5. Delete taxlot annotation from taxmap. This annotation is now feature linked in a
/* new coverage called taxlotan 
/*


ARCEDIT 
EC taxmap
EF anno.igds 
sel $level = 38
DELETE 
SAVE
Q

/* KILL BAD STUFF

&if [exists igds -cover] &then kill igds all
&if [exists taxlab -cover] &then kill taxlab all
&if [exists igdstaxint -cover] &then kill igdstaxint all


&RETURN

